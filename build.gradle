plugins {
	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.9.RELEASE'
	id 'java'
	id 'war'
	id 'groovy'
	id "com.github.node-gradle.node" version "2.2.4"
	id "com.dorongold.task-tree" version "1.5"
	id 'jacoco'
}

group = 'org.monarchinitiative.poet'
version = '1.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}
dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation "org.springframework.boot:spring-boot-starter-actuator"
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	implementation 'org.springframework.security:spring-security-oauth2-resource-server'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'org.springframework.security:spring-security-config'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation group: 'org.monarchinitiative.phenol', name: 'phenol-core', version: '1.6.1'
	implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.4'
	implementation 'org.monarchinitiative.phenol:phenol-annotations:1.6.2'
	implementation 'org.monarchinitiative.phenol:phenol-io:1.6.2'
	implementation 'org.apache.commons:commons-csv:1.9.0'

	runtimeOnly 'com.h2database:h2'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation group: 'org.spockframework', name: 'spock-core', version: '1.3-groovy-2.4'
	testImplementation group: 'org.spockframework', name: 'spock-spring', version: '1.3-groovy-2.4'
}

def profile = 'development'

node {
	version = '16.13.0'
	npmVersion = '8.1.0'
	download = true
	nodeModulesDir = file("src/main/poet")

}

task buildClientProdEnv(type: NpmTask, dependsOn: 'npmInstall') {
	group = 'build'
	description = 'Compile client side assets for production'
	args = ['run', 'buildProd']
}

task buildClientTestEnv(type: NpmTask, dependsOn: 'npmInstall') {
	group = 'build'
	description = 'Compile client side assets for test env'
	args = ['run', 'buildTest']
}

task clientTest(type: NpmTask, dependsOn: 'npmInstall') {
	group = 'verification'
	description = 'Executes client side unit tests'
	args = ['run', 'test']
}

task test_release(){
	dependsOn(test)
	dependsOn(clientTest)
}

test {
	systemProperty 'spring.profiles.active', 'test'
}

test.finalizedBy(jacocoTestReport)

clean {
	delete fileTree(dir: "src/main/resources/static")
}

// Our war files will be versions and tagged with the env
bootWar {
	archiveFileName = "POET-${project.version}-${getActiveProfileTag()}.war_"
}

static def getActiveProfileTag() {
	def profile = System.getProperty('spring.profiles.active')
	switch(profile) {
		case "test":
			profile = "test"
			break
		case "stage":
			profile = "stage"
			break
		case "production":
			profile = "prod"
			break
		default:
			profile = "dev"
			break
	}
	return profile
}

bootWar.dependsOn {
	if(System.getProperty('spring.profiles.active') == "production"){
		buildClientProdEnv
	} else {
		buildClientTestEnv
	}
}

bootRun {
	if(System.getProperty("initialize") == "true"){
		profile = "initialize"
	}
	systemProperty "spring.profiles.active", profile
}